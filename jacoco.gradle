apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.6"
    reportsDir = file("$buildDir/reports")
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    // what to exclude from coverage report
    // UI, "noise", generated classes, platform classes, etc.
    def excludes = [
        '**/R.class',
        '**/R$*.class',
        '**/*$ViewInjector*.*',
        '**/BuildConfig.*',
        '**/di/**',
        '**/adapters/**',
        '**/service/**',
        '**/*Activity.*',
        '**/*App.*',
        '**/*Fragment.*'

    ]
    // generated classes
    classDirectories.from = fileTree(
        dir: "$buildDir/intermediates/classes/debug",
        excludes: excludes
    ) + fileTree(
        dir: "$buildDir/tmp/kotlin-classes/debug",
        excludes: excludes
    )


    // sources
    sourceDirectories.from = files([
        "src/main/kotlin"
    ])
    executionData.from = files("$buildDir/jacoco/testDebugUnitTest.exec")
}
